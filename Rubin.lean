/-
Copyright (c) 2023 Laurent Bartholdi. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author : Laurent Bartholdi
-/
import Mathlib.Data.Finset.Basic
import Mathlib.Data.Finset.Card
import Mathlib.Data.Fintype.Perm
import Mathlib.GroupTheory.Subgroup.Basic
import Mathlib.GroupTheory.Commutator
import Mathlib.GroupTheory.GroupAction.Basic
import Mathlib.GroupTheory.Exponent
import Mathlib.GroupTheory.Perm.Basic
import Mathlib.Topology.Basic
import Mathlib.Topology.Bases
import Mathlib.Topology.Compactness.Compact
import Mathlib.Topology.Separation
import Mathlib.Topology.Homeomorph
import Mathlib.Topology.Algebra.ConstMulAction

import Rubin.Tactic
import Rubin.MulActionExt
import Rubin.SmulImage
import Rubin.Support
import Rubin.Topology
import Rubin.RigidStabilizer
-- import Rubin.RigidStabilizerBasis
import Rubin.Period
import Rubin.AlgebraicDisjointness
import Rubin.RegularSupport
import Rubin.RegularSupportBasis
import Rubin.HomeoGroup
import Rubin.Filter

#align_import rubin

namespace Rubin
open Rubin.Tactic

-- TODO: find a home
theorem equiv_congr_ne {Œπ Œπ' : Type _} (e : Œπ ‚âÉ Œπ') {x y : Œπ} : x ‚â† y ‚Üí e x ‚â† e y :=
  by
  intro x_ne_y
  by_contra h
  apply x_ne_y
  convert congr_arg e.symm h <;> simp only [Equiv.symm_apply_apply]
#align equiv.congr_ne Rubin.equiv_congr_ne

----------------------------------------------------------------
section Rubin

----------------------------------------------------------------
section RubinActions

structure RubinAction (G Œ± : Type _) extends
  Group G,
  TopologicalSpace Œ±,
  MulAction G Œ±,
  FaithfulSMul G Œ±
where
  locally_compact : LocallyCompactSpace Œ±
  hausdorff : T2Space Œ±
  no_isolated_points : HasNoIsolatedPoints Œ±
  locallyDense : LocallyDense G Œ±
#align rubin_action Rubin.RubinAction

end RubinActions

section AlgebraicDisjointness

variable {G Œ± : Type _}
variable [TopologicalSpace Œ±]
variable [Group G]
variable [MulAction G Œ±]
variable [ContinuousConstSMul G Œ±]
variable [FaithfulSMul G Œ±]

-- TODO: modify the proof to be less "let everything"-y, especially the first half
lemma proposition_1_1_1 [h_lm : LocallyMoving G Œ±] [T2Space Œ±] (f g : G) (supp_disjoint : Disjoint (Support Œ± f) (Support Œ± g)) : AlgebraicallyDisjoint f g := by
  apply AlgebraicallyDisjoint_mk
  intros h h_not_commute
  -- h is not the identity on `Support Œ± f`
  have f_h_not_disjoint := (mt (disjoint_commute (G := G) (Œ± := Œ±)) h_not_commute)
  have ‚ü®x, ‚ü®x_in_supp_f, x_in_supp_h‚ü©‚ü© := Set.not_disjoint_iff.mp f_h_not_disjoint
  have hx_ne_x := mem_support.mp x_in_supp_h

  -- Since Œ± is Hausdoff, there is a nonempty V ‚äÜ Support Œ± f, with h ‚Ä¢'' V disjoint from V
  have ‚ü®V, V_open, x_in_V, V_in_support, disjoint_img_V‚ü© := disjoint_nbhd_in (support_isOpen f) x_in_supp_f hx_ne_x

  -- let f‚ÇÇ be a nontrivial element of the RigidStabilizer G V
  let ‚ü®f‚ÇÇ, f‚ÇÇ_in_rist_V, f‚ÇÇ_ne_one‚ü© := h_lm.get_nontrivial_rist_elem V_open (Set.nonempty_of_mem x_in_V)

  -- Re-use the Hausdoff property of Œ± again, this time yielding W ‚äÜ V
  let ‚ü®y, y_moved‚ü© := faithful_moves_point' Œ± f‚ÇÇ_ne_one
  have y_in_V := (rigidStabilizer_support.mp f‚ÇÇ_in_rist_V) (mem_support.mpr y_moved)
  let ‚ü®W, W_open, y_in_W, W_in_V, disjoint_img_W‚ü© := disjoint_nbhd_in V_open y_in_V y_moved

  -- Let f‚ÇÅ be a nontrivial element of RigidStabilizer G W
  let ‚ü®f‚ÇÅ, f‚ÇÅ_in_rist_W, f‚ÇÅ_ne_one‚ü© := h_lm.get_nontrivial_rist_elem W_open (Set.nonempty_of_mem y_in_W)

  use f‚ÇÅ
  use f‚ÇÇ
  constructor <;> try constructor
  ¬∑ apply disjoint_commute (Œ± := Œ±)
    apply Set.disjoint_of_subset_left _ supp_disjoint
    calc
      Support Œ± f‚ÇÅ ‚äÜ W := rigidStabilizer_support.mp f‚ÇÅ_in_rist_W
      W ‚äÜ V := W_in_V
      V ‚äÜ Support Œ± f := V_in_support
  ¬∑ apply disjoint_commute (Œ± := Œ±)
    apply Set.disjoint_of_subset_left _ supp_disjoint
    calc
      Support Œ± f‚ÇÇ ‚äÜ V := rigidStabilizer_support.mp f‚ÇÇ_in_rist_V
      V ‚äÜ Support Œ± f := V_in_support

  -- We claim that [f‚ÇÅ, [f‚ÇÇ, h]] is a nontrivial elelement of Centralizer G g
  let k := ‚ÅÖf‚ÇÇ, h‚ÅÜ
  have h‚ÇÇ : ‚àÄ z ‚àà W, f‚ÇÇ ‚Ä¢ z = k ‚Ä¢ z := by
    intro z z_in_W
    simp
    symm
    apply disjoint_support_comm f‚ÇÇ h _ disjoint_img_V
    ¬∑ exact W_in_V z_in_W
    ¬∑ exact rigidStabilizer_support.mp f‚ÇÇ_in_rist_V

  constructor
  ¬∑ -- then `k*f‚ÇÅ‚Åª¬π*k‚Åª¬π` is supported on k W = f‚ÇÇ W,
    -- so [f‚ÇÅ,k] is supported on W ‚à™ f‚ÇÇ W ‚äÜ V ‚äÜ support f, so commutes with g.
    apply disjoint_commute (Œ± := Œ±)
    apply Set.disjoint_of_subset_left _ supp_disjoint
    have supp_f‚ÇÅ_subset_W := (rigidStabilizer_support.mp f‚ÇÅ_in_rist_W)

    show Support Œ± ‚ÅÖf‚ÇÅ, ‚ÅÖf‚ÇÇ, h‚ÅÜ‚ÅÜ ‚äÜ Support Œ± f
    calc
      Support Œ± ‚ÅÖf‚ÇÅ, k‚ÅÜ = Support Œ± ‚ÅÖk, f‚ÇÅ‚ÅÜ := by rw [<-commutatorElement_inv, support_inv]
      _ ‚äÜ Support Œ± f‚ÇÅ ‚à™ (k ‚Ä¢'' Support Œ± f‚ÇÅ) := support_comm Œ± k f‚ÇÅ
      _ ‚äÜ W ‚à™ (k ‚Ä¢'' Support Œ± f‚ÇÅ) := Set.union_subset_union_left _ supp_f‚ÇÅ_subset_W
      _ ‚äÜ W ‚à™ (k ‚Ä¢'' W) := by
        apply Set.union_subset_union_right
        exact (smulImage_mono k supp_f‚ÇÅ_subset_W)
      _ = W ‚à™ (f‚ÇÇ ‚Ä¢'' W) := by rw [<-smulImage_eq_of_smul_eq h‚ÇÇ]
      _ ‚äÜ V ‚à™ (f‚ÇÇ ‚Ä¢'' W) := Set.union_subset_union_left _ W_in_V
      _ ‚äÜ V ‚à™ V := by
        apply Set.union_subset_union_right
        apply smulImage_subset_in_support f‚ÇÇ W V W_in_V
        exact rigidStabilizer_support.mp f‚ÇÇ_in_rist_V
      _ ‚äÜ V := by rw [Set.union_self]
      _ ‚äÜ Support Œ± f := V_in_support

  ¬∑ -- finally, [f‚ÇÅ,k] agrees with f‚ÇÅ on W, so is not the identity.
    have h‚ÇÑ: ‚àÄ z ‚àà W, ‚ÅÖf‚ÇÅ, k‚ÅÜ ‚Ä¢ z = f‚ÇÅ ‚Ä¢ z := by
      apply disjoint_support_comm f‚ÇÅ k
      exact rigidStabilizer_support.mp f‚ÇÅ_in_rist_W
      rw [<-smulImage_eq_of_smul_eq h‚ÇÇ]
      exact disjoint_img_W
    let ‚ü®z, z_in_W, z_moved‚ü© := faithful_rigid_stabilizer_moves_point f‚ÇÅ_in_rist_W f‚ÇÅ_ne_one

    by_contra h‚ÇÖ
    rw [<-h‚ÇÑ z z_in_W] at z_moved
    have h‚ÇÜ : ‚ÅÖf‚ÇÅ, ‚ÅÖf‚ÇÇ, h‚ÅÜ‚ÅÜ ‚Ä¢ z = z := by rw [h‚ÇÖ, one_smul]
    exact z_moved h‚ÇÜ
#align proposition_1_1_1 Rubin.proposition_1_1_1

lemma moves_1234_of_moves_12 {g : G} {x : Œ±} (g12_moves : g^12 ‚Ä¢ x ‚â† x) :
  Function.Injective (fun i : Fin 5 => g^(i : ‚Ñ§) ‚Ä¢ x) :=
by
  apply moves_inj
  intros k k_ge_1 k_lt_5
  simp at k_lt_5

  by_contra x_fixed
  have k_div_12 : k ‚à£ 12 := by
    -- Note: norm_num does not support ‚Ñ§.dvd yet, nor ‚Ñ§.mod, nor Int.natAbs, nor Int.div, etc.
    have h: (12 : ‚Ñ§) = (12 : ‚Ñï) := by norm_num
    rw [h, Int.ofNat_dvd_right]
    apply Nat.dvd_of_mod_eq_zero

    interval_cases k
    all_goals unfold Int.natAbs
    all_goals norm_num

  have g12_fixed : g^12 ‚Ä¢ x = x := by
    rw [<-zpow_ofNat]
    simp
    rw [<-Int.mul_ediv_cancel' k_div_12]
    have res := smul_zpow_eq_of_smul_eq (12/k) x_fixed
    group_action at res
    exact res

  exact g12_moves g12_fixed

lemma proposition_1_1_2 [T2Space Œ±] [h_lm : LocallyMoving G Œ±]
  (f g : G) (h_disj : AlgebraicallyDisjoint f g) : Disjoint (Support Œ± f) (Support Œ± (g^12)) :=
by
  by_contra not_disjoint
  let U := Support Œ± f ‚à© Support Œ± (g^12)
  have U_nonempty : U.Nonempty := by
    apply Set.not_disjoint_iff_nonempty_inter.mp
    exact not_disjoint

  -- Since G is Hausdorff, we can find a nonempty set V ‚äÜ such that f(V) is disjoint from V and the sets {g^i(V): i=0..4} are pairwise disjoint
  let x := U_nonempty.some
  have x_in_U : x ‚àà U := Set.Nonempty.some_mem U_nonempty
  have fx_moves : f ‚Ä¢ x ‚â† x := Set.inter_subset_left _ _ x_in_U

  have five_points : Function.Injective (fun i : Fin 5 => g^(i : ‚Ñ§) ‚Ä¢ x) := by
    apply moves_1234_of_moves_12
    exact (Set.inter_subset_right _ _ x_in_U)
  have U_open: IsOpen U := (IsOpen.inter (support_isOpen f) (support_isOpen (g^12)))

  let ‚ü®V‚ÇÄ, V‚ÇÄ_open, x_in_V‚ÇÄ, V‚ÇÄ_in_support, disjoint_img_V‚ÇÄ‚ü© := disjoint_nbhd_in U_open x_in_U fx_moves
  let ‚ü®V‚ÇÅ, V‚ÇÅ_open, x_in_V‚ÇÅ, disjoint_img_V‚ÇÅ‚ü© := disjoint_nbhd_fin five_points

  let V := V‚ÇÄ ‚à© V‚ÇÅ
  -- Let h be a nontrivial element of the RigidStabilizer G V
  let ‚ü®h, ‚ü®h_in_ristV, h_ne_one‚ü©‚ü© := h_lm.get_nontrivial_rist_elem (IsOpen.inter V‚ÇÄ_open V‚ÇÅ_open) (Set.nonempty_of_mem ‚ü®x_in_V‚ÇÄ, x_in_V‚ÇÅ‚ü©)

  have V_disjoint_smulImage: Disjoint V (f ‚Ä¢'' V) := by
    apply Set.disjoint_of_subset
    ¬∑ exact Set.inter_subset_left _ _
    ¬∑ apply smulImage_mono
      exact Set.inter_subset_left _ _
    ¬∑ exact disjoint_img_V‚ÇÄ

  have comm_non_trivial : ¬¨Commute f h := by
    by_contra comm_trivial
    let ‚ü®z, z_in_V, z_moved‚ü© := faithful_rigid_stabilizer_moves_point h_in_ristV h_ne_one
    apply z_moved

    nth_rewrite 2 [<-one_smul G z]
    rw [<-commutatorElement_eq_one_iff_commute.mpr comm_trivial.symm]
    symm

    apply disjoint_support_comm h f
    ¬∑ exact rigidStabilizer_support.mp h_in_ristV
    ¬∑ exact V_disjoint_smulImage
    ¬∑ exact z_in_V

  -- Since g is algebraically disjoint from f, there exist f‚ÇÅ,f‚ÇÇ ‚àà C_G(g) so that the commutator h' = [f1,[f2,h]] is a nontrivial element of C_G(g)
  let alg_disj_elem := h_disj h comm_non_trivial
  let f‚ÇÅ := alg_disj_elem.fst
  let f‚ÇÇ := alg_disj_elem.snd
  let h' := alg_disj_elem.comm_elem
  have f‚ÇÅ_commutes : Commute f‚ÇÅ g := alg_disj_elem.fst_commute
  have f‚ÇÇ_commutes : Commute f‚ÇÇ g := alg_disj_elem.snd_commute
  have h'_commutes : Commute h' g := alg_disj_elem.comm_elem_commute
  have h'_nontrivial : h' ‚â† 1 := alg_disj_elem.comm_elem_nontrivial

  have support_f‚ÇÇ_h : Support Œ± ‚ÅÖf‚ÇÇ,h‚ÅÜ ‚äÜ V ‚à™ (f‚ÇÇ ‚Ä¢'' V) := by
    calc
      Support Œ± ‚ÅÖf‚ÇÇ, h‚ÅÜ ‚äÜ Support Œ± h ‚à™ (f‚ÇÇ ‚Ä¢'' Support Œ± h) := support_comm Œ± f‚ÇÇ h
      _ ‚äÜ V ‚à™ (f‚ÇÇ ‚Ä¢'' Support Œ± h) := by
        apply Set.union_subset_union_left
        exact rigidStabilizer_support.mp h_in_ristV
      _ ‚äÜ V ‚à™ (f‚ÇÇ ‚Ä¢'' V) := by
        apply Set.union_subset_union_right
        apply smulImage_mono
        exact rigidStabilizer_support.mp h_in_ristV
  have support_h' : Support Œ± h' ‚äÜ ‚ãÉ(i : Fin 2 √ó Fin 2), (f‚ÇÅ^(i.1.val) * f‚ÇÇ^(i.2.val)) ‚Ä¢'' V := by
    rw [rewrite_Union]
    simp (config := {zeta := false})
    rw [<-smulImage_mul, <-smulImage_union]
    calc
      Support Œ± h' ‚äÜ Support Œ± ‚ÅÖf‚ÇÇ,h‚ÅÜ ‚à™ (f‚ÇÅ ‚Ä¢'' Support Œ± ‚ÅÖf‚ÇÇ, h‚ÅÜ) := support_comm Œ± f‚ÇÅ ‚ÅÖf‚ÇÇ,h‚ÅÜ
      _ ‚äÜ V ‚à™ (f‚ÇÇ ‚Ä¢'' V) ‚à™ (f‚ÇÅ ‚Ä¢'' Support Œ± ‚ÅÖf‚ÇÇ, h‚ÅÜ) := by
        apply Set.union_subset_union_left
        exact support_f‚ÇÇ_h
      _ ‚äÜ V ‚à™ (f‚ÇÇ ‚Ä¢'' V) ‚à™ (f‚ÇÅ ‚Ä¢'' V ‚à™ (f‚ÇÇ ‚Ä¢'' V)) := by
        apply Set.union_subset_union_right
        apply smulImage_mono f‚ÇÅ
        exact support_f‚ÇÇ_h

  -- Since h' is nontrivial, it has at least one point p in its support
  let ‚ü®p, p_moves‚ü© := faithful_moves_point' Œ± h'_nontrivial
  -- Since g commutes with h', all five of the points {gi(p):i=0..4} lie in supp(h')
  have gi_in_support : ‚àÄ (i: Fin 5), g^(i.val) ‚Ä¢ p ‚àà Support Œ± h' := by
    intro i
    rw [mem_support]
    by_contra p_fixed
    rw [<-mul_smul, h'_commutes.pow_right, mul_smul] at p_fixed
    group_action at p_fixed
    exact p_moves p_fixed

  -- The next section gets tricky, so let us clear away some stuff first :3
  clear h'_commutes h'_nontrivial
  clear V‚ÇÄ_open x_in_V‚ÇÄ V‚ÇÄ_in_support disjoint_img_V‚ÇÄ
  clear V‚ÇÅ_open x_in_V‚ÇÅ
  clear five_points h_in_ristV h_ne_one V_disjoint_smulImage
  clear support_f‚ÇÇ_h

  -- by the pigeonhole principle, one of the four sets V, f‚ÇÅ(V), f‚ÇÇ(V), f‚ÇÅf‚ÇÇ(V) must contain two of these points,
  -- say g^i(p),g^j(p) ‚àà k(V) for some 0 ‚â§ i < j ‚â§ 4 and k ‚àà {1,f‚ÇÅ,f‚ÇÇ,f‚ÇÅf‚ÇÇ}
  let pigeonhole : Fintype.card (Fin 5) > Fintype.card (Fin 2 √ó Fin 2) := by trivial
  let choice_pred := fun (i : Fin 5) => (Set.mem_iUnion.mp (support_h' (gi_in_support i)))
  let choice := fun (i : Fin 5) => (choice_pred i).choose
  let ‚ü®i, _, j, _, i_ne_j, same_choice‚ü© := Finset.exists_ne_map_eq_of_card_lt_of_maps_to
    pigeonhole
    (fun (i : Fin 5) _ => Finset.mem_univ (choice i))

  let k := f‚ÇÅ^(choice i).1.val * f‚ÇÇ^(choice i).2.val
  have same_k : f‚ÇÅ^(choice j).1.val * f‚ÇÇ^(choice j).2.val = k := by rw [<-same_choice]
  have gi : g^i.val ‚Ä¢ p ‚àà k ‚Ä¢'' V := (choice_pred i).choose_spec
  have gk : g^j.val ‚Ä¢ p ‚àà k ‚Ä¢'' V := by
    have gk' := (choice_pred j).choose_spec
    rw [same_k] at gk'
    exact gk'

  -- Since g^(j-i)(V) is disjoint from V and k commutes with g,
  -- we know that g^(j‚àíi)k(V) is disjoint from k(V),
  -- which leads to a contradiction since g^i(p) and g^j(p) both lie in k(V).

  have g_disjoint : Disjoint ((g^i.val)‚Åª¬π ‚Ä¢'' V) ((g^j.val)‚Åª¬π ‚Ä¢'' V) := by
    apply smulImage_disjoint_subset (Set.inter_subset_right V‚ÇÄ V‚ÇÅ)
    group
    rw [smulImage_disjoint_inv_pow]
    group
    apply disjoint_img_V‚ÇÅ
    symm; exact i_ne_j

  have k_commutes: Commute k g := by
    apply Commute.mul_left
    ¬∑ exact f‚ÇÅ_commutes.pow_left _
    ¬∑ exact f‚ÇÇ_commutes.pow_left _
  clear f‚ÇÅ_commutes f‚ÇÇ_commutes

  have g_k_disjoint : Disjoint ((g^i.val)‚Åª¬π ‚Ä¢'' (k ‚Ä¢'' V)) ((g^j.val)‚Åª¬π ‚Ä¢'' (k ‚Ä¢'' V)) := by
    repeat rw [smulImage_mul]
    repeat rw [<-inv_pow]
    repeat rw [k_commutes.symm.inv_left.pow_left]
    repeat rw [<-smulImage_mul k]
    repeat rw [inv_pow]
    exact smulImage_disjoint k g_disjoint

  apply Set.disjoint_left.mp g_k_disjoint
  ¬∑ rw [mem_inv_smulImage]
    exact gi
  ¬∑ rw [mem_inv_smulImage]
    exact gk

lemma remark_1_2 (f g : G) (h_disj : AlgebraicallyDisjoint f g): Commute f g := by
  by_contra non_commute
  let disj_elem := h_disj g non_commute
  let nontrivial := disj_elem.comm_elem_nontrivial

  rw [commutatorElement_eq_one_iff_commute.mpr disj_elem.snd_commute] at nontrivial
  rw [commutatorElement_one_right] at nontrivial

  tauto

end AlgebraicDisjointness

section RegularSupport

lemma lemma_2_2 (G: Type _) {Œ± : Type _} [Group G] [TopologicalSpace Œ±] [MulAction G Œ±]
  [ContinuousConstSMul G Œ±] [FaithfulSMul G Œ±]
  [T2Space Œ±] [h_lm : LocallyMoving G Œ±]
  {U : Set Œ±} (U_open : IsOpen U) (U_nonempty : Set.Nonempty U) :
  Monoid.exponent G‚Ä¢[U] = 0 :=
by
  by_contra exp_ne_zero

  let ‚ü®p, ‚ü®g, g_in_ristU‚ü©, n, p_in_U, n_pos, hpgn, n_eq_Sup‚ü© := Period.period_from_exponent U U_nonempty exp_ne_zero
  simp at hpgn
  let ‚ü®V', V'_open, p_in_V', disj'‚ü© := disjoint_nbhd_fin (smul_injective_within_period hpgn)

  let V := U ‚à© V'
  have V_open : IsOpen V := U_open.inter V'_open
  have p_in_V : p ‚àà V := ‚ü®p_in_U, p_in_V'‚ü©
  have disj : ‚àÄ (i j : Fin n), i ‚â† j ‚Üí Disjoint (g ^ (i : ‚Ñï) ‚Ä¢'' V) (g ^ (j : ‚Ñï) ‚Ä¢'' V) := by
    intro i j i_ne_j
    apply Set.disjoint_of_subset
    ¬∑ apply smulImage_mono
      apply Set.inter_subset_right
    ¬∑ apply smulImage_mono
      apply Set.inter_subset_right
    exact disj' i j i_ne_j

  let ‚ü®h, h_in_ristV, h_ne_one‚ü© := h_lm.get_nontrivial_rist_elem V_open (Set.nonempty_of_mem p_in_V)
  have hg_in_ristU : h * g ‚àà RigidStabilizer G U := by
    simp [RigidStabilizer]
    intro x x_notin_U
    rw [mul_smul]
    rw [g_in_ristU _ x_notin_U]
    have x_notin_V : x ‚àâ V := fun x_in_V => x_notin_U x_in_V.left
    rw [h_in_ristV _ x_notin_V]
  let ‚ü®q, q_in_V, hq_ne_q ‚ü© := faithful_rigid_stabilizer_moves_point h_in_ristV h_ne_one
  have gpowi_q_notin_V : ‚àÄ (i : Fin n), (i : ‚Ñï) > 0 ‚Üí g ^ (i : ‚Ñï) ‚Ä¢ q ‚àâ V := by
    apply smulImage_distinct_of_disjoint_pow n_pos disj
    exact q_in_V

  -- We have (hg)^i q = g^i q for all 0 < i < n
  have hgpow_eq_gpow : ‚àÄ (i : Fin n), (h * g) ^ (i : ‚Ñï) ‚Ä¢ q = g ^ (i : ‚Ñï) ‚Ä¢ q := by
    intro ‚ü®i, i_lt_n‚ü©
    simp
    induction i with
    | zero => simp
    | succ i' IH =>
      have i'_lt_n: i' < n := Nat.lt_of_succ_lt i_lt_n
      have IH := IH i'_lt_n
      rw [smul_succ]
      rw [IH]
      rw [smul_succ]
      rw [mul_smul]
      rw [<-smul_succ]

      -- We can show that `g^(Nat.succ i') ‚Ä¢ q ‚àâ V`,
      -- which means that with `h` in `RigidStabilizer G V`, `h` fixes that point
      apply h_in_ristV (g^(Nat.succ i') ‚Ä¢ q)

      let i'‚ÇÇ : Fin n := ‚ü®Nat.succ i', i_lt_n‚ü©
      have h_eq: Nat.succ i' = (i'‚ÇÇ : ‚Ñï) := by simp
      rw [h_eq]
      apply smulImage_distinct_of_disjoint_pow
      ¬∑ exact n_pos
      ¬∑ exact disj
      ¬∑ exact q_in_V
      ¬∑ simp

  -- Combined with `g^i ‚Ä¢ q ‚â† q`, this yields `(hg)^i ‚Ä¢ q ‚â† q` for all `0 < i < n`
  have hgpow_moves : ‚àÄ (i : Fin n), 0 < (i : ‚Ñï) ‚Üí (h*g)^(i : ‚Ñï) ‚Ä¢ q ‚â† q := by
    intro ‚ü®i, i_lt_n‚ü© i_pos
    simp at i_pos
    rw [hgpow_eq_gpow]
    simp
    by_contra h'
    apply gpowi_q_notin_V ‚ü®i, i_lt_n‚ü©
    exact i_pos
    simp (config := {zeta := false}) only []
    rw [h']
    exact q_in_V

  -- This even holds for `i = n`
  have hgpown_moves : (h * g) ^ n ‚Ä¢ q ‚â† q := by
    -- Rewrite (hg)^n ‚Ä¢ q = h * g^n ‚Ä¢ q
    rw [<-Nat.succ_pred n_pos.ne.symm]
    rw [pow_succ]
    have h_eq := hgpow_eq_gpow ‚ü®Nat.pred n, Nat.pred_lt_self n_pos‚ü©
    simp at h_eq
    rw [mul_smul, h_eq, <-mul_smul, mul_assoc, <-pow_succ]
    rw [<-Nat.succ_eq_add_one, Nat.succ_pred n_pos.ne.symm]

    -- We first eliminate `g^n ‚Ä¢ q` by proving that `n = Period g q`
    have period_gq_eq_n : Period.period q g = n := by
      apply ge_antisymm
      {
        apply Period.notfix_le_period'
        ¬∑ exact n_pos
        ¬∑ apply Period.period_pos'
          ¬∑ exact Set.nonempty_of_mem p_in_U
          ¬∑ exact exp_ne_zero
          ¬∑ exact q_in_V.left
          ¬∑ exact g_in_ristU
        ¬∑ intro i i_pos
          rw [<-hgpow_eq_gpow]
          apply hgpow_moves i i_pos
      }
      {
        rw [n_eq_Sup]
        apply Period.period_le_Sup_periods'
        ¬∑ exact Set.nonempty_of_mem p_in_U
        ¬∑ exact exp_ne_zero
        ¬∑ exact q_in_V.left
        ¬∑ exact g_in_ristU
      }

    rw [mul_smul, <-period_gq_eq_n]
    rw [Period.pow_period_fix]
    -- Finally, we have `h ‚Ä¢ q ‚â† q`
    exact hq_ne_q

  -- Finally, we derive a contradiction
  have ‚ü®period_hg_pos, period_hg_le_n‚ü© := Period.zero_lt_period_le_Sup_periods U_nonempty exp_ne_zero ‚ü®q, q_in_V.left‚ü© ‚ü®h * g, hg_in_ristU‚ü©
  simp at period_hg_pos
  simp at period_hg_le_n
  rw [<-n_eq_Sup] at period_hg_le_n
  cases (lt_or_eq_of_le period_hg_le_n) with
  | inl period_hg_lt_n =>
      apply hgpow_moves ‚ü®Period.period q (h * g), period_hg_lt_n‚ü©
      exact period_hg_pos
      simp
      apply Period.pow_period_fix
  | inr period_hg_eq_n =>
      apply hgpown_moves
      rw [<-period_hg_eq_n]
      apply Period.pow_period_fix


-- Given the statement `¬¨Support Œ± h ‚äÜ RegularSupport Œ± f`,
-- we construct an open subset within `Support Œ± h \ RegularSupport Œ± f`,
-- and we show that it is non-empty, open and (by construction) disjoint from `Support Œ± f`.
lemma open_set_from_supp_not_subset_rsupp {G Œ± : Type _}
  [Group G] [TopologicalSpace Œ±] [MulAction G Œ±] [ContinuousConstSMul G Œ±] [T2Space Œ±]
  {f h : G} (not_support_subset_rsupp : ¬¨Support Œ± h ‚äÜ RegularSupport Œ± f):
  ‚àÉ V : Set Œ±, V ‚äÜ Support Œ± h ‚àß Set.Nonempty V ‚àß IsOpen V ‚àß Disjoint V (Support Œ± f) :=
by
  let U := Support Œ± h \ closure (RegularSupport Œ± f)
  have U_open : IsOpen U := by
    unfold_let
    rw [Set.diff_eq_compl_inter]
    apply IsOpen.inter
    ¬∑ simp
    ¬∑ exact support_isOpen _
  have U_subset_supp_h : U ‚äÜ Support Œ± h := by simp; apply Set.diff_subset
  have U_disj_supp_f : Disjoint U (Support Œ± f) := by
    apply Set.disjoint_of_subset_right
    ¬∑ exact subset_closure
    ¬∑ simp
      rw [Set.diff_eq_compl_inter]
      apply Disjoint.inter_left
      apply Disjoint.closure_right; swap; simp

      rw [Set.disjoint_compl_left_iff_subset]
      apply subset_trans
      exact subset_closure
      apply closure_mono
      apply support_subset_regularSupport

  have U_nonempty : Set.Nonempty U; swap
  exact ‚ü®U, U_subset_supp_h, U_nonempty, U_open, U_disj_supp_f‚ü©

  -- We prove that U isn't empty by contradiction:
  -- if it is empty, then `Support Œ± h \ closure (RegularSupport Œ± f) = ‚àÖ`,
  -- so we can show that `Support Œ± h ‚äÜ RegularSupport Œ± f`,
  -- contradicting with our initial hypothesis.
  by_contra U_empty
  apply not_support_subset_rsupp
  show Support Œ± h ‚äÜ RegularSupport Œ± f

  apply subset_from_diff_closure_eq_empty
  ¬∑ apply regularSupport_regular
  ¬∑ exact support_isOpen _
  ¬∑ rw [Set.not_nonempty_iff_eq_empty] at U_empty
    exact U_empty

lemma nontrivial_pow_from_exponent_eq_zero {G : Type _} [Group G]
  (exp_eq_zero : Monoid.exponent G = 0) :
  ‚àÄ (n : ‚Ñï), n > 0 ‚Üí ‚àÉ g : G, g^n ‚â† 1 :=
by
  intro n n_pos
  rw [Monoid.exponent_eq_zero_iff] at exp_eq_zero
  unfold Monoid.ExponentExists at exp_eq_zero
  rw [<-Classical.not_forall_not, Classical.not_not] at exp_eq_zero
  simp at exp_eq_zero
  exact exp_eq_zero n n_pos


lemma proposition_2_1 {G Œ± : Type _}
  [Group G] [TopologicalSpace Œ±] [MulAction G Œ±] [ContinuousConstSMul G Œ±] [T2Space Œ±]
  [LocallyMoving G Œ±] [h_faithful : FaithfulSMul G Œ±]
  (f : G) :
  AlgebraicCentralizer f = G‚Ä¢[RegularSupport Œ± f] :=
by
  ext h

  constructor
  swap
  {
    intro h_in_rist
    simp at h_in_rist
    unfold AlgebraicCentralizer
    rw [Subgroup.mem_centralizer_iff]
    intro g g_in_S
    simp [AlgebraicSubgroup] at g_in_S
    let ‚ü®g', ‚ü®g'_alg_disj, g_eq_g'‚ü©‚ü© := g_in_S
    have supp_disj := proposition_1_1_2 f g' g'_alg_disj (Œ± := Œ±)

    apply Commute.eq
    symm
    apply commute_if_rigidStabilizer_and_disjoint (Œ± := Œ±)
    ¬∑ exact h_in_rist
    ¬∑ show Disjoint (RegularSupport Œ± f) (Support Œ± g)
      have cl_supp_disj : Disjoint (closure (Support Œ± f)) (Support Œ± g)
      swap

      apply Set.disjoint_of_subset _ _ cl_supp_disj
      ¬∑ rw [RegularSupport.def]
        exact interior_subset
      ¬∑ rfl
      ¬∑ rw [<-g_eq_g']
        exact Disjoint.closure_left supp_disj (support_isOpen _)
  }

  intro h_in_centralizer
  by_contra h_notin_rist
  simp at h_notin_rist
  rw [rigidStabilizer_support] at h_notin_rist
  let ‚ü®V, V_in_supp_h, V_nonempty, V_open, V_disj_supp_f‚ü© := open_set_from_supp_not_subset_rsupp h_notin_rist
  let ‚ü®v, v_in_V‚ü© := V_nonempty
  have v_moved := V_in_supp_h v_in_V
  rw [mem_support] at v_moved

  have ‚ü®W, W_open, v_in_W, W_subset_support, disj_W_img‚ü© := disjoint_nbhd_in V_open v_in_V v_moved

  have mono_exp := lemma_2_2 G W_open (Set.nonempty_of_mem v_in_W)
  let ‚ü®‚ü®g, g_in_rist‚ü©, g12_ne_one‚ü© := nontrivial_pow_from_exponent_eq_zero mono_exp 12 (by norm_num)
  simp at g12_ne_one

  have disj_supports : Disjoint (Support Œ± f) (Support Œ± g) := by
    apply Set.disjoint_of_subset_right
    ¬∑ apply rigidStabilizer_support.mp
      exact g_in_rist
    ¬∑ apply Set.disjoint_of_subset_right
      ¬∑ exact W_subset_support
      ¬∑ exact V_disj_supp_f.symm
  have alg_disj_f_g := proposition_1_1_1 _ _ disj_supports
  have g12_in_algebraic_subgroup : g^12 ‚àà AlgebraicSubgroup f := by
    simp [AlgebraicSubgroup]
    use g
    constructor
    exact ‚Üëalg_disj_f_g
    rfl

  have h_nc_g12 : ¬¨Commute (g^12) h := by
    have supp_g12_sub_W : Support Œ± (g^12) ‚äÜ W := by
      rw [rigidStabilizer_support] at g_in_rist
      calc
        Support Œ± (g^12) ‚äÜ Support Œ± g := by apply support_pow
        _ ‚äÜ W := g_in_rist
    have supp_g12_disj_hW : Disjoint (Support Œ± (g^12)) (h ‚Ä¢'' W) := by
      apply Set.disjoint_of_subset_left
      swap
      ¬∑ exact disj_W_img
      ¬∑ exact supp_g12_sub_W

    exact not_commute_of_disj_support_smulImage
      g12_ne_one
      supp_g12_sub_W
      supp_g12_disj_hW

  apply h_nc_g12
  exact h_in_centralizer _ g12_in_algebraic_subgroup

-- Small lemma for remark 2.3
theorem rigidStabilizer_inter_bot_iff_regularSupport_disj {G Œ± : Type _}
  [Group G] [TopologicalSpace Œ±] [MulAction G Œ±] [ContinuousConstSMul G Œ±] [LocallyMoving G Œ±] [FaithfulSMul G Œ±]
  {f g : G} :
  G‚Ä¢[RegularSupport Œ± f] ‚äì G‚Ä¢[RegularSupport Œ± g] = ‚ä•
  ‚Üî Disjoint (RegularSupport Œ± f) (RegularSupport Œ± g) :=
by
  rw [<-rigidStabilizer_inter]
  constructor
  {
    intro rist_disj

    by_contra rsupp_not_disj
    rw [Set.not_disjoint_iff] at rsupp_not_disj
    let ‚ü®x, x_in_rsupp_f, x_in_rsupp_g‚ü© := rsupp_not_disj

    have rsupp_open: IsOpen (RegularSupport Œ± f ‚à© RegularSupport Œ± g) := by
      apply IsOpen.inter <;> exact regularSupport_open _ _

    -- The contradiction occurs by applying the definition of LocallyMoving:
    apply LocallyMoving.locally_moving (G := G) _ rsupp_open _ rist_disj

    exact ‚ü®x, x_in_rsupp_f, x_in_rsupp_g‚ü©
  }
  {
    intro rsupp_disj
    rw [Set.disjoint_iff_inter_eq_empty] at rsupp_disj
    rw [rsupp_disj]

    by_contra rist_ne_bot
    rw [<-ne_eq, Subgroup.ne_bot_iff_exists_ne_one] at rist_ne_bot
    let ‚ü®‚ü®h, h_in_rist‚ü©, h_ne_one‚ü© := rist_ne_bot
    simp at h_ne_one
    apply h_ne_one
    rw [rigidStabilizer_empty] at h_in_rist
    rw [Subgroup.mem_bot] at h_in_rist
    exact h_in_rist
  }

variable {G Œ± : Type _}
variable [Group G]
variable [TopologicalSpace Œ±] [T2Space Œ±]
variable [MulAction G Œ±] [ContinuousConstSMul G Œ±] [FaithfulSMul G Œ±] [LocallyMoving G Œ±]

/--
This demonstrates that the disjointness of the supports of two elements `f` and `g`
can be proven without knowing anything about how `f` and `g` act on `Œ±`
(bar the more global properties of the group action).

We could prove that the intersection of the algebraic centralizers of `f` and `g` is trivial
purely within group theory, and then apply this theorem to know that their support
in `Œ±` will be disjoint.
--/
lemma remark_2_3 {f g : G} :
  (AlgebraicCentralizer f) ‚äì (AlgebraicCentralizer g) = ‚ä• ‚Üí Disjoint (Support Œ± f) (Support Œ± g) :=
by
  intro alg_disj
  rw [disjoint_interiorClosure_iff (support_isOpen _) (support_isOpen _)]
  simp
  repeat rw [<-RegularSupport.def]
  rw [<-rigidStabilizer_inter_bot_iff_regularSupport_disj]

  repeat rw [<-proposition_2_1]
  exact alg_disj

-- lemma remark_2_3' {f g : G} :
--   (AlgebraicCentralizer f) ‚äì (AlgebraicCentralizer g) ‚â† ‚ä• ‚Üí
--   Set.Nonempty ((RegularSupport Œ± f) ‚à© (RegularSupport Œ± g)) :=
-- by
--   intro alg_inter_neBot
--   repeat rw [proposition_2_1 (Œ± := Œ±)] at alg_inter_neBot
--   rw [ne_eq] at alg_inter_neBot

--   rw [rigidStabilizer_inter_bot_iff_regularSupport_disj] at alg_inter_neBot
--   rw [Set.not_disjoint_iff_nonempty_inter] at alg_inter_neBot
--   exact alg_inter_neBot

lemma rigidStabilizer_inter_eq_algebraicCentralizerInter {S : Finset G} :
  G‚Ä¢[RegularSupport.FiniteInter Œ± S] = AlgebraicCentralizerInter S :=
by
  unfold RegularSupport.FiniteInter
  unfold AlgebraicCentralizerInter
  rw [rigidStabilizer_iInter_regularSupport']
  simp only [<-proposition_2_1]

lemma regularSupportInter_nonEmpty_iff_neBot {S : Finset G} [Nonempty Œ±]:
  AlgebraicCentralizerInter S ‚â† ‚ä• ‚Üî
  Set.Nonempty (RegularSupport.FiniteInter Œ± S) :=
by
  constructor
  ¬∑ rw [<-rigidStabilizer_inter_eq_algebraicCentralizerInter (Œ± := Œ±), ne_eq]
    intro rist_neBot
    by_contra eq_empty
    rw [Set.not_nonempty_iff_eq_empty] at eq_empty
    rw [eq_empty, rigidStabilizer_empty] at rist_neBot
    exact rist_neBot rfl
  ¬∑ intro nonempty
    intro eq_bot
    rw [<-rigidStabilizer_inter_eq_algebraicCentralizerInter (Œ± := Œ±)] at eq_bot
    rw [<-rigidStabilizer_empty (G := G) (Œ± := Œ±), rigidStabilizer_eq_iff] at eq_bot
    ¬∑ rw [eq_bot, Set.nonempty_iff_ne_empty] at nonempty
      exact nonempty rfl
    ¬∑ apply RegularSupport.FiniteInter_regular
    ¬∑ simp

theorem AlgebraicCentralizerBasis.exists_rigidStabilizer_inv (H : Set G) [Nonempty Œ±]:
  ‚àÉ S,
    (H ‚àà AlgebraicCentralizerBasis G ‚Üí S ‚àà RegularSupportBasis G Œ± ‚àß H = G‚Ä¢[S])
    ‚àß (H ‚àâ AlgebraicCentralizerBasis G ‚Üí S = ‚àÖ) :=
by
  by_cases H_in_basis?: H ‚àà AlgebraicCentralizerBasis G
  swap
  {
    use ‚àÖ
    constructor
    tauto
    intro _
    rfl
  }

  have ‚ü®H_ne_one, ‚ü®seed, H_eq‚ü©‚ü© := (AlgebraicCentralizerBasis.mem_iff H).mp H_in_basis?

  rw [H_eq, <-Subgroup.coe_bot, ne_eq, SetLike.coe_set_eq, <-ne_eq] at H_ne_one

  use RegularSupport.FiniteInter Œ± seed
  constructor
  ¬∑ intro _
    rw [RegularSupportBasis.mem_iff]
    repeat' apply And.intro
    ¬∑ rw [<-regularSupportInter_nonEmpty_iff_neBot]
      exact H_ne_one
    ¬∑ use seed
    ¬∑ rw [rigidStabilizer_inter_eq_algebraicCentralizerInter]
      exact H_eq
  ¬∑ tauto

theorem AlgebraicCentralizerBasis.mem_of_regularSupportBasis {S : Set Œ±}
  (S_in_basis : S ‚àà RegularSupportBasis G Œ±) :
  (G‚Ä¢[S] : Set G) ‚àà AlgebraicCentralizerBasis G :=
by
  rw [AlgebraicCentralizerBasis.subgroup_mem_iff]
  rw [RegularSupportBasis.mem_iff] at S_in_basis
  let ‚ü®S_nonempty, ‚ü®seed, S_eq‚ü©‚ü© := S_in_basis

  have Œ±_nonempty : Nonempty Œ± := by
    by_contra Œ±_empty
    rw [not_nonempty_iff] at Œ±_empty
    rw [Set.nonempty_iff_ne_empty] at S_nonempty
    apply S_nonempty
    exact Set.eq_empty_of_isEmpty S

  constructor
  ¬∑ rw [S_eq, rigidStabilizer_inter_eq_algebraicCentralizerInter]
    rw [regularSupportInter_nonEmpty_iff_neBot (Œ± := Œ±)]
    rw [<-S_eq]
    exact S_nonempty
  ¬∑ use seed
    rw [S_eq]
    exact rigidStabilizer_inter_eq_algebraicCentralizerInter

@[simp]
theorem AlgebraicCentralizerBasis.eq_rist_image [Nonempty Œ±]:
  (fun S => (G‚Ä¢[S] : Set G)) '' RegularSupportBasis G Œ± = AlgebraicCentralizerBasis G :=
by
  ext H
  constructor
  ¬∑ simp
    intro S S_in_basis H_eq
    rw [<-H_eq]
    apply mem_of_regularSupportBasis S_in_basis
  ¬∑ intro H_in_basis
    simp

    let ‚ü®S, ‚ü®S_props, _‚ü©‚ü© := AlgebraicCentralizerBasis.exists_rigidStabilizer_inv (Œ± := Œ±) H
    let ‚ü®S_in_basis, H_eq‚ü© := S_props H_in_basis
    symm at H_eq
    use S

noncomputable def rigidStabilizer_inv [Nonempty Œ±] (H : Set G) : Set Œ± :=
  (AlgebraicCentralizerBasis.exists_rigidStabilizer_inv H).choose

theorem rigidStabilizer_inv_eq [Nonempty Œ±] {H : Set G} (H_in_basis : H ‚àà AlgebraicCentralizerBasis G) :
  H = G‚Ä¢[rigidStabilizer_inv (Œ± := Œ±) H] :=
by
  have spec := (AlgebraicCentralizerBasis.exists_rigidStabilizer_inv (Œ± := Œ±) H).choose_spec
  exact (spec.left H_in_basis).right

theorem rigidStabilizer_inv_in_basis [Nonempty Œ±] (H : Set G) :
  H ‚àà AlgebraicCentralizerBasis G ‚Üî rigidStabilizer_inv (Œ± := Œ±) H ‚àà RegularSupportBasis G Œ± :=
by
  have spec := (AlgebraicCentralizerBasis.exists_rigidStabilizer_inv (Œ± := Œ±) H).choose_spec
  constructor
  ¬∑ intro H_in_basis
    exact (spec.left H_in_basis).left
  ¬∑ intro iH_in_basis
    by_contra H_notin_basis
    unfold rigidStabilizer_inv at iH_in_basis
    rw [(spec.right H_notin_basis)] at iH_in_basis
    exact RegularSupportBasis.empty_not_mem G Œ± iH_in_basis

theorem rigidStabilizer_inv_eq' [Nonempty Œ±] {S : Set Œ±} (S_in_basis : S ‚àà RegularSupportBasis G Œ±) :
  rigidStabilizer_inv (Œ± := Œ±) (G := G) G‚Ä¢[S] = S :=
by
  have GS_in_basis : (G‚Ä¢[S] : Set G) ‚àà AlgebraicCentralizerBasis G := by
    exact AlgebraicCentralizerBasis.mem_of_regularSupportBasis S_in_basis

  have eq := rigidStabilizer_inv_eq GS_in_basis (Œ± := Œ±)
  rw [SetLike.coe_set_eq, rigidStabilizer_eq_iff] at eq
  ¬∑ exact eq.symm
  ¬∑ exact RegularSupportBasis.regular S_in_basis
  ¬∑ exact RegularSupportBasis.regular ((rigidStabilizer_inv_in_basis _).mp GS_in_basis)

variable [Nonempty Œ±] [HasNoIsolatedPoints Œ±] [LocallyDense G Œ±]

noncomputable def RigidStabilizer.order_iso_on (G Œ± : Type _) [Group G] [Nonempty Œ±] [TopologicalSpace Œ±] [T2Space Œ±]
  [MulAction G Œ±] [ContinuousConstSMul G Œ±] [FaithfulSMul G Œ±]
  [HasNoIsolatedPoints Œ±] [LocallyDense G Œ±] : OrderIsoOn (Set Œ±) (Set G) (RegularSupportBasis G Œ±)
where
  toFun := fun S => G‚Ä¢[S]
  invFun := fun H => rigidStabilizer_inv (Œ± := Œ±) H

  leftInv_on := by
    intro S S_in_basis
    simp
    exact rigidStabilizer_inv_eq' S_in_basis

  rightInv_on := by
    intro H H_in_basis
    simp
    simp at H_in_basis
    symm
    exact rigidStabilizer_inv_eq H_in_basis

  toFun_doubleMonotone := by
    intro S S_in_basis T T_in_basis
    simp
    apply rigidStabilizer_subset_iff G (RegularSupportBasis.regular S_in_basis) (RegularSupportBasis.regular T_in_basis)

@[simp]
theorem RigidStabilizer.order_iso_on_toFun:
  (RigidStabilizer.order_iso_on G Œ±).toFun = (fun S => (G‚Ä¢[S] : Set G)) :=
by
  simp [order_iso_on]

@[simp]
theorem RigidStabilizer.order_iso_on_invFun:
  (RigidStabilizer.order_iso_on G Œ±).invFun = (fun S => rigidStabilizer_inv (Œ± := Œ±) S) :=
by
  simp [order_iso_on]

noncomputable def RigidStabilizer.inv_order_iso_on (G Œ± : Type _) [Group G] [Nonempty Œ±] [TopologicalSpace Œ±] [T2Space Œ±]
  [MulAction G Œ±] [ContinuousConstSMul G Œ±] [FaithfulSMul G Œ±]
  [HasNoIsolatedPoints Œ±] [LocallyDense G Œ±] : OrderIsoOn (Set G) (Set Œ±) (AlgebraicCentralizerBasis G) :=
  (RigidStabilizer.order_iso_on G Œ±).inv.mk_of_subset
    (subset_of_eq (AlgebraicCentralizerBasis.eq_rist_image (Œ± := Œ±) (G := G)).symm)

@[simp]
theorem RigidStabilizer.inv_order_iso_on_toFun:
  (RigidStabilizer.inv_order_iso_on G Œ±).toFun = (fun S => rigidStabilizer_inv (Œ± := Œ±) S) :=
by
  simp [inv_order_iso_on, order_iso_on]

@[simp]
theorem RigidStabilizer.inv_order_iso_on_invFun:
  (RigidStabilizer.inv_order_iso_on G Œ±).invFun = (fun S => (G‚Ä¢[S] : Set G)) :=
by
  simp [inv_order_iso_on, order_iso_on]

-- TODO: mark simp theorems as local
@[simp]
theorem RegularSupportBasis.eq_inv_rist_image:
  (fun H => rigidStabilizer_inv (Œ± := Œ±) H) '' AlgebraicCentralizerBasis G = RegularSupportBasis G Œ± :=
by
  rw [<-AlgebraicCentralizerBasis.eq_rist_image (Œ± := Œ±) (G := G)]
  rw [Set.image_image]
  nth_rw 2 [<-OrderIsoOn.leftInv_image (RigidStabilizer.order_iso_on G Œ±)]
  rw [Function.comp_def]
  rw [RigidStabilizer.order_iso_on]

lemma RigidStabilizer_doubleMonotone : DoubleMonotoneOn (fun S => G‚Ä¢[S]) (RegularSupportBasis G Œ±) := by
  have res := (RigidStabilizer.order_iso_on G Œ±).toFun_doubleMonotone
  simp at res
  exact res

lemma RigidStabilizer_inv_doubleMonotone : DoubleMonotoneOn (fun S => rigidStabilizer_inv (Œ± := Œ±) S) (AlgebraicCentralizerBasis G) := by
  have res := (RigidStabilizer.order_iso_on G Œ±).invFun_doubleMonotone
  simp at res
  exact res

lemma RigidStabilizer_rightInv {U : Set G} (U_in_basis : U ‚àà AlgebraicCentralizerBasis G) :
  G‚Ä¢[rigidStabilizer_inv (Œ± := Œ±) U] = U :=
by
  have res := (RigidStabilizer.order_iso_on G Œ±).rightInv_on U
  simp at res
  exact res U_in_basis

lemma RigidStabilizer_leftInv {U : Set Œ±} (U_in_basis : U ‚àà RegularSupportBasis G Œ±) :
  rigidStabilizer_inv (Œ± := Œ±) (G‚Ä¢[U] : Set G) = U :=
by
  have res := (RigidStabilizer.order_iso_on G Œ±).leftInv_on U
  simp at res
  exact res U_in_basis


theorem rigidStabilizer_subset_iff_subset_inv [Nonempty Œ±] {S : Set Œ±} (S_in_basis : S ‚àà RegularSupportBasis G Œ±) {T : Set G} (T_in_basis : T ‚àà AlgebraicCentralizerBasis G):
  (G‚Ä¢[S] : Set G) ‚äÜ T ‚Üî S ‚äÜ rigidStabilizer_inv T :=
by
  nth_rw 1 [<-RigidStabilizer_rightInv (Œ± := Œ±) T_in_basis]
  rw [SetLike.coe_subset_coe]
  rw [rigidStabilizer_subset_iff (G := G)]
  ¬∑ exact RegularSupportBasis.regular S_in_basis
  ¬∑ apply RegularSupportBasis.regular (G := G)
    rw [<-rigidStabilizer_inv_in_basis T]
    assumption

theorem subset_rigidStabilizer_iff_inv_subset [Nonempty Œ±] {S : Set G} (S_in_basis : S ‚àà AlgebraicCentralizerBasis G) {T : Set Œ±} (T_in_basis : T ‚àà RegularSupportBasis G Œ±):
  S ‚äÜ (G‚Ä¢[T] : Set G) ‚Üî rigidStabilizer_inv S ‚äÜ T :=
by
  nth_rw 1 [<-RigidStabilizer_rightInv (Œ± := Œ±) S_in_basis]
  rw [SetLike.coe_subset_coe]
  rw [rigidStabilizer_subset_iff (G := G)]
  ¬∑ apply RegularSupportBasis.regular (G := G)
    rw [<-rigidStabilizer_inv_in_basis S]
    assumption
  ¬∑ exact RegularSupportBasis.regular T_in_basis

theorem rigidStabilizer_inv_smulImage [Nonempty Œ±] {S : Set G} (S_in_basis : S ‚àà AlgebraicCentralizerBasis G) (h : G) :
  h ‚Ä¢'' rigidStabilizer_inv S = rigidStabilizer_inv (Œ± := Œ±) ((fun g => h * g * h‚Åª¬π) '' S) :=
by
  rw [smulImage_inv]
  rw [<-rigidStabilizer_eq_iff (G := G)]
  swap
  {
    apply RegularSupportBasis.regular (G := G)
    rw [<-rigidStabilizer_inv_in_basis S]
    exact S_in_basis
  }
  swap
  {
    rw [<-smulImage_regular]
    apply RegularSupportBasis.regular (G := G)
    rw [<-rigidStabilizer_inv_in_basis]
    apply AlgebraicCentralizerBasis.conj_mem
    assumption
  }
  rw [<-SetLike.coe_set_eq]
  rw [<-rigidStabilizer_conj_image_eq]
  repeat rw [RigidStabilizer_rightInv]
  ¬∑ rw [Set.image_image]
    group
    simp
  ¬∑ apply AlgebraicCentralizerBasis.conj_mem
    assumption
  ¬∑ assumption

end RegularSupport

section HomeoGroup

open Topology

variable {G Œ± : Type _} [Group G] [TopologicalSpace Œ±] [T2Space Œ±]
variable [MulAction G Œ±] [ContinuousConstSMul G Œ±] [FaithfulSMul G Œ±] [LocallyMoving G Œ±]

theorem exists_compact_closure_of_le_nhds {Œ± : Type _} [TopologicalSpace Œ±] [T2Space Œ±] [LocallyCompactSpace Œ±] (F : Filter Œ±):
  (‚àÉ p : Œ±, F ‚â§ ùìù p) ‚Üí ‚àÉ S ‚àà F, IsCompact (closure S) :=
by
  intro ‚ü®p, p_le_nhds‚ü©
  have ‚ü®S, S_in_nhds, S_compact‚ü© := (compact_basis_nhds p).ex_mem
  use S
  constructor
  exact p_le_nhds S_in_nhds
  rw [IsClosed.closure_eq S_compact.isClosed]
  exact S_compact

theorem clusterPt_of_exists_compact_closure {Œ± : Type _} [TopologicalSpace Œ±] [T2Space Œ±] [LocallyCompactSpace Œ±] (F : Filter Œ±) [Filter.NeBot F]:
  (‚àÉ S ‚àà F, IsCompact (closure S)) ‚Üí ‚àÉ p : Œ±, ClusterPt p F :=
by
  intro ‚ü®S, S_in_F, clS_compact‚ü©
  have F_le_principal_S : F ‚â§ Filter.principal (closure S) := by
    rw [Filter.le_principal_iff]
    apply Filter.sets_of_superset
    exact S_in_F
    exact subset_closure
  let ‚ü®x, _, F_clusterPt‚ü© := clS_compact F_le_principal_S
  use x

theorem proposition_3_4_2 {Œ± : Type _} [TopologicalSpace Œ±] [T2Space Œ±] [LocallyCompactSpace Œ±] (F : Ultrafilter Œ±):
  (‚àÉ p : Œ±, ClusterPt p F) ‚Üî ‚àÉ S ‚àà F, IsCompact (closure S) :=
by
  constructor
  ¬∑ simp only [Ultrafilter.clusterPt_iff, <-Ultrafilter.mem_coe]
    exact exists_compact_closure_of_le_nhds (F : Filter Œ±)
  ¬∑ exact clusterPt_of_exists_compact_closure (F : Filter Œ±)

end HomeoGroup


section Ultrafilter

variable {G Œ± : Type _}
variable [Group G]
variable [TopologicalSpace Œ±] [T2Space Œ±]
variable [MulAction G Œ±] [ContinuousConstSMul G Œ±] [FaithfulSMul G Œ±] [LocallyMoving G Œ±]

def RSuppSubsets (G : Type _) {Œ± : Type _} [Group G] [TopologicalSpace Œ±] [MulAction G Œ±] (V : Set Œ±) : Set (Set Œ±) :=
  {W ‚àà RegularSupportBasis G Œ± | W ‚äÜ V}

def RSuppOrbit {G Œ± : Type _} [Group G] [TopologicalSpace Œ±] [MulAction G Œ±] (F : Filter Œ±) (H : Subgroup G) : Set (Set Œ±) :=
  { g ‚Ä¢'' W | (g ‚àà H) (W ‚àà F) }

lemma moving_elem_of_open_subset_closure_orbit {U V : Set Œ±} (U_open : IsOpen U) (U_nonempty : Set.Nonempty U)
  {p : Œ±} (U_ss_clOrbit : U ‚äÜ closure (MulAction.orbit G‚Ä¢[V] p)) :
  ‚àÉ h : G, h ‚àà G‚Ä¢[V] ‚àß h ‚Ä¢ p ‚àà U :=
by
  have p_in_orbit : p ‚àà MulAction.orbit G‚Ä¢[V] p := by simp

  have ‚ü®q, ‚ü®q_in_U, q_in_orbit‚ü©‚ü© := inter_of_open_subset_of_closure
    U_open U_nonempty ‚ü®p, p_in_orbit‚ü© U_ss_clOrbit

  rw [MulAction.mem_orbit_iff] at q_in_orbit
  let ‚ü®‚ü®h, h_in_orbit‚ü©, hq_eq_p‚ü© := q_in_orbit
  simp at hq_eq_p

  use h
  constructor
  assumption
  rw [hq_eq_p]
  assumption

lemma compact_subset_of_rsupp_basis (G : Type _) {Œ± : Type _}
  [Group G] [TopologicalSpace Œ±] [T2Space Œ±]
  [MulAction G Œ±] [ContinuousConstSMul G Œ±]
  [LocallyCompactSpace Œ±] [HasNoIsolatedPoints Œ±] [LocallyDense G Œ±]
  {U : Set Œ±} (U_in_basis : U ‚àà RegularSupportBasis G Œ±):
  ‚àÉ V : RegularSupportBasis G Œ±, (closure V.val) ‚äÜ U ‚àß IsCompact (closure V.val) :=
by
  let ‚ü®‚ü®x, x_in_U‚ü©, _‚ü© := (RegularSupportBasis.mem_iff U).mp U_in_basis
  have U_regular : Regular U := RegularSupportBasis.regular U_in_basis

  let ‚ü®W, W_compact, x_in_intW, W_ss_U‚ü© := exists_compact_subset U_regular.isOpen x_in_U
  have ‚ü®V, V_in_basis, _, V_ss_intW‚ü© := (RegularSupportBasis.isBasis G Œ±).exists_subset_of_mem_open x_in_intW isOpen_interior

  have clV_ss_W : closure V ‚äÜ W := by
    calc
      closure V ‚äÜ closure (interior W) := by
        apply closure_mono
        exact V_ss_intW
      _ ‚äÜ closure W := by
        apply closure_mono
        exact interior_subset
      _ = W := by
        apply IsClosed.closure_eq
        exact W_compact.isClosed

  use ‚ü®V, V_in_basis‚ü©
  simp

  constructor
  ¬∑ exact subset_trans clV_ss_W W_ss_U
  ¬∑ exact IsCompact.of_isClosed_subset W_compact isClosed_closure clV_ss_W

variable [LocallyDense G Œ±] [LocallyCompactSpace Œ±] [HasNoIsolatedPoints Œ±]

lemma proposition_3_5_1
  {U : Set Œ±} (U_in_basis : U ‚àà RegularSupportBasis G Œ±) (F: Filter Œ±):
  (‚àÉ p ‚àà U, F ‚â§ nhds p)
  ‚Üí ‚àÉ V : RegularSupportBasis G Œ±, V.val ‚äÜ U ‚àß RSuppSubsets G V.val ‚äÜ RSuppOrbit F G‚Ä¢[U] :=
by
  simp
  intro p p_in_U F_le_nhds_p
  have U_regular : Regular U := RegularSupportBasis.regular U_in_basis

  -- First, get a neighborhood of p that is a subset of the closure of the orbit of G_U
  have clOrbit_in_nhds := LocallyDense.rigidStabilizer_in_nhds G Œ± U_regular.isOpen p_in_U
  rw [mem_nhds_iff] at clOrbit_in_nhds
  let ‚ü®V, V_ss_clOrbit, V_open, p_in_V‚ü© := clOrbit_in_nhds
  clear clOrbit_in_nhds

  -- Then, get a nontrivial element from that set
  let ‚ü®g, g_in_rist, g_ne_one‚ü© := LocallyMoving.get_nontrivial_rist_elem (G := G) V_open ‚ü®p, p_in_V‚ü©

  have V_ss_clU : V ‚äÜ closure U := by
    apply subset_trans
    exact V_ss_clOrbit
    apply closure_mono
    exact orbit_rigidStabilizer_subset p_in_U

  -- The regular support of g is within U
  have rsupp_ss_U : RegularSupport Œ± g ‚äÜ U := by
    rw [RegularSupport]
    rw [rigidStabilizer_support] at g_in_rist
    calc
      InteriorClosure (Support Œ± g) ‚äÜ InteriorClosure V := by
        apply interiorClosure_mono
        assumption
      _ ‚äÜ InteriorClosure (closure U) := by
        apply interiorClosure_mono
        assumption
      _ ‚äÜ InteriorClosure U := by
        simp
        rfl
      _ ‚äÜ _ := by
        apply subset_of_eq
        exact U_regular

  let T := RegularSupportBasis.fromSingleton (Œ± := Œ±) g g_ne_one
  have T_eq : T.val = RegularSupport Œ± g := by
    unfold_let
    rw [RegularSupportBasis.fromSingleton_val]
  use T.val

  repeat' apply And.intro
  ¬∑ -- This statement is equivalent to rsupp(g) ‚äÜ U
    rw [T_eq]
    exact rsupp_ss_U
  ¬∑ exact T.prop.left
  ¬∑ exact T.prop.right
  ¬∑ intro W W_in_subsets
    simp [RSuppSubsets, T_eq] at W_in_subsets
    let ‚ü®W_in_basis, W_ss_W‚ü© := W_in_subsets
    unfold RSuppOrbit
    simp

    -- We have that W is a subset of the closure of the orbit of G_U
    have W_ss_clOrbit : W ‚äÜ closure (MulAction.orbit G‚Ä¢[U] p) := by
      rw [rigidStabilizer_support] at g_in_rist
      calc
        W ‚äÜ RegularSupport Œ± g := by assumption
        _ ‚äÜ closure (Support Œ± g) := regularSupport_subset_closure_support
        _ ‚äÜ closure V := by
          apply closure_mono
          assumption
        _ ‚äÜ _ := by
          rw [<-closure_closure (s := MulAction.orbit _ _)]
          apply closure_mono
          assumption

    let ‚ü®W_nonempty, ‚ü®W_seed, W_eq‚ü©‚ü© := W_in_basis
    have W_regular := RegularSupportBasis.regular W_in_basis

    -- So we can get an element `h` such that `h ‚Ä¢ p ‚àà W` and `h ‚àà G_U`
    let ‚ü®h, h_in_rist, hp_in_W‚ü© := moving_elem_of_open_subset_closure_orbit W_regular.isOpen W_nonempty W_ss_clOrbit

    use h
    constructor
    exact h_in_rist

    use h‚Åª¬π ‚Ä¢'' W
    constructor
    swap
    {
      rw [smulImage_mul]
      simp
    }

    -- We just need to show that h‚Åª¬π ‚Ä¢'' W ‚àà F, that is, h‚Åª¬π ‚Ä¢'' W ‚àà ùìù p
    apply F_le_nhds_p

    have basis := (RegularSupportBasis.isBasis G Œ±).nhds_hasBasis (a := p)
    rw [basis.mem_iff]
    use h‚Åª¬π ‚Ä¢'' W
    repeat' apply And.intro
    ¬∑ rw [smulImage_nonempty]
      assumption
    ¬∑ simp only [smulImage_inv, inv_inv]
      have dec_eq : DecidableEq G := Classical.typeDecidableEq G
      use Finset.image (fun g => h‚Åª¬π * g * h) W_seed
      rw [<-RegularSupport.FiniteInter_conj, Finset.image_image]
      have fn_eq_id : (fun g => h * g * h‚Åª¬π) ‚àò (fun g => h‚Åª¬π * g * h) = id := by
        ext x
        simp
        group
      rw [fn_eq_id, Finset.image_id]
      exact W_eq
    ¬∑ rw [mem_smulImage, inv_inv]
      exact hp_in_W
    ¬∑ exact Eq.subset rfl

theorem proposition_3_5_2
  {U : Set Œ±} (F: Filter Œ±) [Filter.NeBot F]:
  (‚àÉ V : RegularSupportBasis G Œ±, V.val ‚äÜ U ‚àß RSuppSubsets G V.val ‚äÜ RSuppOrbit F G‚Ä¢[U]) ‚Üí ‚àÉ p ‚àà U, ClusterPt p F :=
by
  intro ‚ü®‚ü®V, V_in_basis‚ü©, ‚ü®V_ss_U, subsets_ss_orbit‚ü©‚ü©
  simp only at V_ss_U
  simp only at subsets_ss_orbit

  -- Obtain a compact subset of V' in the basis
  let ‚ü®V', clV'_ss_V, clV'_compact‚ü© := compact_subset_of_rsupp_basis G V_in_basis

  have V'_in_subsets : V'.val ‚àà RSuppSubsets G V := by
    unfold RSuppSubsets
    simp
    exact subset_trans subset_closure clV'_ss_V

  -- V' is in the orbit, so there exists a value `g ‚àà G_U` such that `gV ‚àà F`
  -- Note that with the way we set up the equations, we obtain `g‚Åª¬π`
  have V'_in_orbit := subsets_ss_orbit V'_in_subsets
  simp [RSuppOrbit] at V'_in_orbit
  let ‚ü®g, g_in_rist, ‚ü®W, W_in_F, gW_eq_V‚ü©‚ü© := V'_in_orbit

  have gV'_in_F : g‚Åª¬π ‚Ä¢'' V' ‚àà F := by
    rw [smulImage_inv] at gW_eq_V
    rw [<-gW_eq_V]
    assumption
  have gV'_compact : IsCompact (closure (g‚Åª¬π ‚Ä¢'' V'.val)) := by
    rw [smulImage_closure]
    apply smulImage_compact
    assumption

  have ‚ü®p, p_lim‚ü© := clusterPt_of_exists_compact_closure _ ‚ü®g‚Åª¬π ‚Ä¢'' V'.val, ‚ü®gV'_in_F, gV'_compact‚ü©‚ü©
  use p
  constructor
  swap
  assumption

  rw [clusterPt_iff_forall_mem_closure] at p_lim
  specialize p_lim (g‚Åª¬π ‚Ä¢'' V') gV'_in_F
  rw [smulImage_closure, mem_smulImage, inv_inv] at p_lim

  rw [rigidStabilizer_support, <-support_inv] at g_in_rist
  rw [<-fixed_smulImage_in_support g‚Åª¬π g_in_rist]

  rw [mem_smulImage, inv_inv]
  apply V_ss_U
  apply clV'_ss_V
  exact p_lim

/--
# Proposition 3.5

This proposition gives an alternative definition for an ultrafilter to converge within a set `U`.
This alternative definition should be reconstructible entirely from the algebraic structure of `G`.
--/
theorem proposition_3_5 {U : Set Œ±} (U_in_basis : U ‚àà RegularSupportBasis G Œ±) (F: Ultrafilter Œ±):
  (‚àÉ p ‚àà U, ClusterPt p F)
  ‚Üî ‚àÉ V : RegularSupportBasis G Œ±, V.val ‚äÜ U ‚àß RSuppSubsets G V.val ‚äÜ RSuppOrbit F G‚Ä¢[U] :=
by
  constructor
  ¬∑ simp only [Ultrafilter.clusterPt_iff]
    exact proposition_3_5_1 U_in_basis (F : Filter Œ±)
  ¬∑ exact proposition_3_5_2 (F : Filter Œ±)

theorem proposition_3_5' {U : Set Œ±} (U_in_basis : U ‚àà RegularSupportBasis G Œ±)
  (F : UltrafilterInBasis (RegularSupportBasis G Œ±)):
  (‚àÉ p ‚àà U, F ‚â§ nhds p)
  ‚Üî ‚àÉ V ‚àà RegularSupportBasis G Œ±, V ‚äÜ U ‚àß RSuppSubsets G V ‚äÜ RSuppOrbit F G‚Ä¢[U] :=
by
  constructor
  ¬∑ intro ex_p
    let ‚ü®‚ü®V, V_in_basis‚ü©, V_ss_U, subsets_ss_orbit‚ü© := proposition_3_5_1 U_in_basis (F : Filter Œ±) ex_p
    exact ‚ü®V, V_in_basis, V_ss_U, subsets_ss_orbit‚ü©
  ¬∑ intro ‚ü®V, V_in_basis, V_ss_U, subsets_ss_orbit‚ü©
    simp only [
      <-F.clusterPt_iff_le_nhds
        (RegularSupportBasis.isBasis G Œ±)
        (RegularSupportBasis.closed_inter G Œ±)
    ]
    exact proposition_3_5_2 (F : Filter Œ±) ‚ü®‚ü®V, V_in_basis‚ü©, V_ss_U, subsets_ss_orbit‚ü©

end Ultrafilter

section RubinFilter

variable {G : Type _} [Group G]
variable {Œ± : Type _} [Nonempty Œ±] [TopologicalSpace Œ±] [HasNoIsolatedPoints Œ±] [T2Space Œ±] [LocallyCompactSpace Œ±]
variable [MulAction G Œ±] [ContinuousConstSMul G Œ±] [FaithfulSMul G Œ±] [LocallyDense G Œ±]

def AlgebraicSubsets (V : Set G) : Set (Set G) :=
  {W ‚àà AlgebraicCentralizerBasis G | W ‚äÜ V}

def AlgebraicOrbit (F : Filter G) (U : Set G) : Set (Set G) :=
  { (fun h => g * h * g‚Åª¬π) '' W | (g ‚àà U) (W ‚àà F.sets ‚à© AlgebraicCentralizerBasis G) }

theorem AlgebraicOrbit.mem_iff (F : Filter G) (U : Set G) (S : Set G):
  S ‚àà AlgebraicOrbit F U ‚Üî ‚àÉ g ‚àà U, ‚àÉ W ‚àà F, W ‚àà AlgebraicCentralizerBasis G ‚àß S = (fun h => g * h * g‚Åª¬π) '' W :=
by
  simp [AlgebraicOrbit]
  simp only [and_assoc, eq_comm]

def AlgebraicConvergent {G : Type _} [Group G]
  (F : Filter G)
  (U : Set G) : Prop :=
  ‚àÉ V ‚àà AlgebraicCentralizerBasis G, V ‚äÜ U ‚àß AlgebraicSubsets V ‚äÜ AlgebraicOrbit F U

structure RubinFilter (G : Type _) [Group G] :=
  filter : UltrafilterInBasis (AlgebraicCentralizerBasis G)

  converges : AlgebraicConvergent filter.filter Set.univ

lemma RegularSupportBasis.empty_not_mem' : ‚àÖ ‚àâ (RigidStabilizer.inv_order_iso_on G Œ±).toFun '' AlgebraicCentralizerBasis G := by
  simp
  exact RegularSupportBasis.empty_not_mem _ _

lemma AlgebraicCentralizerBasis.empty_not_mem' : ‚àÖ ‚àâ (RigidStabilizer.order_iso_on G Œ±).toFun '' RegularSupportBasis G Œ± := by
  unfold RigidStabilizer.order_iso_on
  rw [AlgebraicCentralizerBasis.eq_rist_image]
  exact AlgebraicCentralizerBasis.empty_not_mem

def RubinFilter.map (F : RubinFilter G) (Œ± : Type _)
  [TopologicalSpace Œ±] [T2Space Œ±] [Nonempty Œ±] [HasNoIsolatedPoints Œ±]
  [MulAction G Œ±] [ContinuousConstSMul G Œ±] [FaithfulSMul G Œ±] [LocallyDense G Œ±] : UltrafilterInBasis (RegularSupportBasis G Œ±) :=
  (
    F.filter.map_basis
      AlgebraicCentralizerBasis.empty_not_mem
      (RigidStabilizer.inv_order_iso_on G Œ±)
      RegularSupportBasis.empty_not_mem'
  ).cast (by simp)

def IsRubinFilterOf (A : UltrafilterInBasis (RegularSupportBasis G Œ±)) (B : UltrafilterInBasis (AlgebraicCentralizerBasis G)) : Prop :=
  ‚àÄ U ‚àà RegularSupportBasis G Œ±, U ‚àà A ‚Üî (G‚Ä¢[U] : Set G) ‚àà B

theorem RubinFilter.map_isRubinFilterOf (F : RubinFilter G) :
  IsRubinFilterOf (F.map Œ±) F.filter :=
by
  intro U U_in_basis
  unfold map
  simp
  have ‚ü®U', U'_in_basis, U'_eq‚ü© := (RegularSupportBasis.eq_inv_rist_image (G := G) (Œ± := Œ±)).symm ‚ñ∏ U_in_basis
  simp only at U'_eq
  rw [<-U'_eq]
  rw [Filter.InBasis.map_mem_map_basis_of_basis_set _ RigidStabilizer_inv_doubleMonotone F.filter.in_basis U'_in_basis]
  rw [RigidStabilizer_rightInv U'_in_basis]
  rfl

theorem RubinFilter.from_isRubinFilterOf' (F : UltrafilterInBasis (RegularSupportBasis G Œ±)) :
  IsRubinFilterOf F ((F.map_basis
    (RegularSupportBasis.empty_not_mem G Œ±)
    (RigidStabilizer.order_iso_on G Œ±)
    AlgebraicCentralizerBasis.empty_not_mem'
  ).cast (by simp)) :=
by
  intro U U_in_basis
  simp
  rw [Filter.InBasis.map_mem_map_basis_of_basis_set _ RigidStabilizer_doubleMonotone F.in_basis U_in_basis]
  rfl

theorem IsRubinFilterOf.mem_inv {A : UltrafilterInBasis (RegularSupportBasis G Œ±)}
  {B : UltrafilterInBasis (AlgebraicCentralizerBasis G)}
  (filter_of : IsRubinFilterOf A B) {U : Set G} (U_in_basis : U ‚àà AlgebraicCentralizerBasis G):
  U ‚àà B ‚Üî rigidStabilizer_inv U ‚àà A :=
by
  rw [<-AlgebraicCentralizerBasis.eq_rist_image (Œ± := Œ±)] at U_in_basis
  let ‚ü®V, V_in_basis, V_eq‚ü© := U_in_basis
  rw [<-V_eq, RigidStabilizer_leftInv V_in_basis]
  symm
  exact filter_of V V_in_basis

theorem IsRubinFilterOf.mem_inter_inv {A : UltrafilterInBasis (RegularSupportBasis G Œ±)}
  {B : UltrafilterInBasis (AlgebraicCentralizerBasis G)}
  (filter_of : IsRubinFilterOf A B) (U : Set G):
  U ‚àà B.filter.sets ‚à© AlgebraicCentralizerBasis G ‚Üî rigidStabilizer_inv U ‚àà A.filter.sets ‚à© RegularSupportBasis G Œ± :=
by
  constructor
  ¬∑ intro ‚ü®U_in_filter, U_in_basis‚ü©
    constructor
    simp
    rw [<-filter_of.mem_inv U_in_basis]
    exact U_in_filter
    rw [<-rigidStabilizer_inv_in_basis]
    assumption
  ¬∑ intro ‚ü®iU_in_filter, U_in_basis‚ü©
    rw [<-rigidStabilizer_inv_in_basis] at U_in_basis
    constructor
    ¬∑ simp
      rw [filter_of.mem_inv U_in_basis]
      exact iU_in_filter
    ¬∑ assumption

theorem IsRubinFilterOf.subsets_ss_orbit {A : UltrafilterInBasis (RegularSupportBasis G Œ±)}
  {B : UltrafilterInBasis (AlgebraicCentralizerBasis G)}
  (filter_of : IsRubinFilterOf A B)
  {V : Set Œ±} -- (V_in_basis : V ‚àà RegularSupportBasis G Œ±)
  {W : Set Œ±} (W_in_basis : W ‚àà RegularSupportBasis G Œ±) :
  RSuppSubsets G W ‚äÜ RSuppOrbit A G‚Ä¢[V] ‚Üî AlgebraicSubsets (G‚Ä¢[W]) ‚äÜ AlgebraicOrbit B.filter G‚Ä¢[V] :=
by
  have dec_eq : DecidableEq G := Classical.typeDecidableEq _

  constructor
  ¬∑ intro rsupp_ss
    unfold AlgebraicSubsets AlgebraicOrbit
    intro U ‚ü®U_in_basis, U_ss_GW‚ü©
    let U' := rigidStabilizer_inv (Œ± := Œ±) U
    have U'_in_basis : U' ‚àà RegularSupportBasis G Œ± := (rigidStabilizer_inv_in_basis U).mp U_in_basis
    have U'_ss_W : U' ‚äÜ W := by
      rw [subset_rigidStabilizer_iff_inv_subset U_in_basis W_in_basis] at U_ss_GW
      exact U_ss_GW
    let ‚ü®g, g_in_GV, ‚ü®W, W_in_A, gW_eq_U‚ü©‚ü© := rsupp_ss ‚ü®U'_in_basis, U'_ss_W‚ü©
    use g
    constructor
    {
      simp
      exact g_in_GV
    }

    have W_in_basis : W ‚àà RegularSupportBasis G Œ± := by
      rw [smulImage_inv] at gW_eq_U
      rw [gW_eq_U]
      apply RegularSupportBasis.smulImage_in_basis
      assumption

    use G‚Ä¢[W]
    rw [filter_of.mem_inter_inv]
    rw [RigidStabilizer_leftInv (G := G) (Œ± := Œ±) W_in_basis]
    refine ‚ü®‚ü®W_in_A, W_in_basis‚ü©, ?W_eq_U‚ü©
    rw [rigidStabilizer_conj_image_eq, gW_eq_U]
    unfold_let
    exact RigidStabilizer_rightInv U_in_basis
  ¬∑ intro algsupp_ss
    unfold RSuppSubsets RSuppOrbit
    simp
    intro U U_in_basis U_ss_W
    let U' := (G‚Ä¢[U] : Set G)
    have U'_in_basis : U' ‚àà AlgebraicCentralizerBasis G :=
      AlgebraicCentralizerBasis.mem_of_regularSupportBasis U_in_basis
    have U'_ss_GW : U' ‚äÜ G‚Ä¢[W] := by
      rw [SetLike.coe_subset_coe]
      rw [<-rigidStabilizer_subset_iff]
      ¬∑ assumption
      ¬∑ exact RegularSupportBasis.regular U_in_basis
      ¬∑ exact RegularSupportBasis.regular W_in_basis

    let ‚ü®g, g_in_GV, ‚ü®X, X_in_inter, X_eq‚ü©‚ü© := algsupp_ss ‚ü®U'_in_basis, U'_ss_GW‚ü©
    have X_in_basis := X_in_inter.right
    rw [filter_of.mem_inter_inv] at X_in_inter

    simp at g_in_GV
    use g
    refine ‚ü®g_in_GV, ‚ü®rigidStabilizer_inv X, X_in_inter.left, ?giX_eq_U‚ü©‚ü©

    apply (And.right) at X_in_inter
    rw [rigidStabilizer_inv_smulImage X_in_basis, X_eq]
    unfold_let
    rw [RigidStabilizer_leftInv U_in_basis]

theorem IsRubinFilterOf.converges_iff {A : UltrafilterInBasis (RegularSupportBasis G Œ±)}
  {B : UltrafilterInBasis (AlgebraicCentralizerBasis G)}
  (filter_of : IsRubinFilterOf A B)
  {V : Set Œ±} (V_in_basis : V ‚àà RegularSupportBasis G Œ±)
  :
  (‚àÉ p ‚àà V, A ‚â§ nhds p) ‚Üî
  AlgebraicConvergent B.filter G‚Ä¢[V] :=
by
  unfold AlgebraicConvergent
  constructor
  ¬∑ rw [proposition_3_5' V_in_basis]
    intro ‚ü®W, W_in_basis, W_ss_V, subsets_ss_orbit‚ü©
    use G‚Ä¢[W]
    rw [<-filter_of.subsets_ss_orbit W_in_basis]
    refine ‚ü®?GW_in_basis, ?GW_ss_GV, subsets_ss_orbit‚ü©
    exact AlgebraicCentralizerBasis.mem_of_regularSupportBasis W_in_basis
    simp
    rwa [<-rigidStabilizer_subset_iff _ (RegularSupportBasis.regular W_in_basis) (RegularSupportBasis.regular V_in_basis)]
  ¬∑ intro ‚ü®W, W_in_basis, W_ss_GV, subsets_ss_orbit‚ü©
    rw [<-AlgebraicCentralizerBasis.eq_rist_image (Œ± := Œ±)] at W_in_basis
    let ‚ü®W', W'_in_basis, W'_eq‚ü© := W_in_basis
    simp only at W'_eq
    rw [proposition_3_5' V_in_basis]
    use W'
    rw [filter_of.subsets_ss_orbit W'_in_basis, W'_eq]
    refine ‚ü®W'_in_basis, ?W'_ss_V, subsets_ss_orbit‚ü©
    rw [<-W'_eq] at W_ss_GV
    simp at W_ss_GV
    rwa [<-rigidStabilizer_subset_iff _ (RegularSupportBasis.regular W'_in_basis) (RegularSupportBasis.regular V_in_basis)] at W_ss_GV

def RubinFilter.from (F : UltrafilterInBasis (RegularSupportBasis G Œ±)) (F_converges : ‚àÉ p : Œ±, F ‚â§ nhds p) : RubinFilter G where
  filter := (F.map_basis
    (RegularSupportBasis.empty_not_mem G Œ±)
    (RigidStabilizer.order_iso_on G Œ±)
    AlgebraicCentralizerBasis.empty_not_mem'
  ).cast (by simp)

  converges := by
    let ‚ü®p, F_le_nhds‚ü© := F_converges

    have ‚ü®W, W_in_basis, _, W_subsets_ss_GV_orbit‚ü© := (proposition_3_5' (RegularSupportBasis.univ_mem G Œ±) F).mp ‚ü®p, (Set.mem_univ p), F_le_nhds‚ü©

    refine ‚ü®
      G‚Ä¢[W],
      by apply AlgebraicCentralizerBasis.mem_of_regularSupportBasis W_in_basis,
      by simp,
      ?subsets_ss_orbit
    ‚ü©

    rw [<-Subgroup.coe_top, <-rigidStabilizer_univ (Œ± := Œ±) (G := G)]
    rwa [<-(RubinFilter.from_isRubinFilterOf' F).subsets_ss_orbit W_in_basis]


theorem RubinFilter.from_isRubinFilterOf (F : UltrafilterInBasis (RegularSupportBasis G Œ±)) (F_converges : ‚àÉ p : Œ±, F ‚â§ nhds p):
  IsRubinFilterOf F (RubinFilter.from F F_converges).filter := RubinFilter.from_isRubinFilterOf' F

theorem RubinFilter.map_from_eq (F : UltrafilterInBasis (RegularSupportBasis G Œ±)) (F_converges : ‚àÉ p : Œ±, F ‚â§ nhds p):
  (RubinFilter.from F F_converges).map Œ± = F :=
by
  apply UltrafilterInBasis.eq_of_le
  apply UltrafilterInBasis.le_of_basis_sets
  intro S S_in_B S_in_F

  rw [(RubinFilter.from_isRubinFilterOf F F_converges) S S_in_B] at S_in_F
  rw [(RubinFilter.map_isRubinFilterOf (RubinFilter.from F F_converges) (Œ± := Œ±)) S S_in_B]

  exact S_in_F

section Convergence

variable (Œ± : Type _)
variable [Nonempty Œ±] [TopologicalSpace Œ±] [T2Space Œ±] [HasNoIsolatedPoints Œ±] [LocallyCompactSpace Œ±]
variable [MulAction G Œ±] [ContinuousConstSMul G Œ±] [FaithfulSMul G Œ±] [LocallyDense G Œ±]

theorem RubinFilter.map_converges (F : RubinFilter G):
  ‚àÉ p : Œ±, (F.map Œ±).filter ‚â§ nhds p :=
by
  suffices ‚àÉ p ‚àà Set.univ, (F.map Œ±).filter ‚â§ nhds p by
    let ‚ü®p, _, f_le_nhds‚ü© := this
    exact ‚ü®p, f_le_nhds‚ü©

  rw [proposition_3_5' (RegularSupportBasis.univ_mem G Œ±)]
  let ‚ü®V, V_in_basis, _, subsets_ss_orbit‚ü© := F.converges
  simp only [Set.subset_univ, true_and, Subtype.exists, exists_prop]
  use rigidStabilizer_inv V
  refine ‚ü®(rigidStabilizer_inv_in_basis V).mp V_in_basis, ?subsets_ss_orbit‚ü©
  rw [(RubinFilter.map_isRubinFilterOf F (Œ± := Œ±)).subsets_ss_orbit
    ((rigidStabilizer_inv_in_basis V).mp V_in_basis)
  ]
  rw [RigidStabilizer_rightInv V_in_basis]
  simp
  exact subsets_ss_orbit

theorem RubinFilter.from_map_eq (F : RubinFilter G):
  RubinFilter.from (F.map Œ±) (RubinFilter.map_converges Œ± F) = F :=
by
  rw [mk.injEq]
  apply UltrafilterInBasis.eq_of_le
  apply UltrafilterInBasis.le_of_basis_sets
  intro S S_in_B S_in_F

  rw [(RubinFilter.from_isRubinFilterOf (F.map Œ±) (RubinFilter.map_converges Œ± F)).mem_inv S_in_B]
  rw [<-(RubinFilter.map_isRubinFilterOf F (Œ± := Œ±)).mem_inv S_in_B]
  exact S_in_F

noncomputable def RubinFilter.lim (F : RubinFilter G)
  : Œ± := Exists.choose (RubinFilter.map_converges F (Œ± := Œ±))

theorem RubinFilter.le_nhds_lim (F : RubinFilter G) :
  F.map Œ± ‚â§ nhds (F.lim Œ±) := (RubinFilter.map_converges F (Œ± := Œ±)).choose_spec

theorem RubinFilter.le_nhds_eq_lim (F : RubinFilter G) (p : Œ±) :
  F.map Œ± ‚â§ nhds p ‚Üí p = F.lim Œ± :=
by
  intro F_le_p
  have F_le_lim := F.le_nhds_lim (Œ± := Œ±)
  by_contra p_ne_lim
  rw [<-ne_eq, <-disjoint_nhds_nhds] at p_ne_lim
  apply (map F Œ±).ne_bot.ne
  exact Filter.empty_mem_iff_bot.mp (p_ne_lim F_le_p F_le_lim trivial)

lemma RubinFilter.lim_mem_iff (F : RubinFilter G) {T : Set Œ±} (T_in_basis : T ‚àà RegularSupportBasis G Œ±) :
  F.lim Œ± ‚àà T ‚Üî ‚àÉ V ‚àà RegularSupportBasis G Œ±, V ‚äÜ T ‚àß RSuppSubsets G V ‚äÜ RSuppOrbit (F.map Œ±) G‚Ä¢[T] :=
by
  rw [<-proposition_3_5' T_in_basis]

  constructor
  ¬∑ intro lim_in_T
    use lim Œ± F
    exact ‚ü®lim_in_T, le_nhds_lim Œ± F‚ü©
  ¬∑ intro ‚ü®p, p_in_T, le_nhds_p‚ü©
    exact (F.le_nhds_eq_lim Œ± p le_nhds_p) ‚ñ∏ p_in_T

lemma RubinFilter.exists_nhds_iff_lim_in_set (F : RubinFilter G) (T : Set Œ±) :
  (‚àÉ p ‚àà T, F.map Œ± ‚â§ nhds p) ‚Üî F.lim Œ± ‚àà T :=
by
  constructor
  ¬∑ intro ‚ü®p, p_in_T, F_le_nhds‚ü©
    convert p_in_T
    exact (F.le_nhds_eq_lim Œ± p F_le_nhds).symm
  ¬∑ intro lim_in_T
    exact ‚ü®lim Œ± F, lim_in_T, le_nhds_lim Œ± F‚ü©

end Convergence

section Setoid

/--
Two rubin filters are equivalent if they share the same behavior in regards to which set their converging point `p` lies in.
--/
instance RubinFilterSetoid (G : Type _) [Group G] : Setoid (RubinFilter G) where
  r F F' := ‚àÄ (U : Set G), U ‚àà AlgebraicCentralizerBasis G ‚Üí
    (AlgebraicConvergent F.filter U ‚Üî AlgebraicConvergent F'.filter U)
  iseqv := by
    constructor
    ¬∑ intros
      simp
    ¬∑ intro F F' h
      intro U U_rigid
      symm
      exact h U U_rigid
    ¬∑ intro F‚ÇÅ F‚ÇÇ F‚ÇÉ
      intro h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ
      intro U U_rigid
      specialize h‚ÇÅ‚ÇÇ U U_rigid
      specialize h‚ÇÇ‚ÇÉ U U_rigid
      exact Iff.trans h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ

lemma RubinFilter.lim_mem_iff_of_eqv {F‚ÇÅ F‚ÇÇ : RubinFilter G} (F_equiv : F‚ÇÅ ‚âà F‚ÇÇ)
  {S : Set Œ±} (S_in_basis : S ‚àà RegularSupportBasis G Œ±) :
  F‚ÇÅ.lim Œ± ‚àà S ‚Üî F‚ÇÇ.lim Œ± ‚àà S
:= by
  have F‚ÇÅ_rubinFilterOf := (RubinFilter.map_isRubinFilterOf F‚ÇÅ (Œ± := Œ±))
  have F‚ÇÇ_rubinFilterOf := (RubinFilter.map_isRubinFilterOf F‚ÇÇ (Œ± := Œ±))

  rw [F‚ÇÅ.lim_mem_iff Œ± S_in_basis, <-proposition_3_5' S_in_basis]
  rw [F‚ÇÅ_rubinFilterOf.converges_iff S_in_basis]
  rw [F_equiv _ (AlgebraicCentralizerBasis.mem_of_regularSupportBasis S_in_basis)]
  rw [<-F‚ÇÇ_rubinFilterOf.converges_iff S_in_basis]
  rw [F‚ÇÇ.lim_mem_iff Œ± S_in_basis, <-proposition_3_5' S_in_basis]

lemma RubinFilter.mem_nhds_lim_iff_of_eqv {F‚ÇÅ F‚ÇÇ : RubinFilter G} (F_equiv : F‚ÇÅ ‚âà F‚ÇÇ)
  (S : Set Œ±) : S ‚àà nhds (F‚ÇÅ.lim Œ±) ‚Üî S ‚àà nhds (F‚ÇÇ.lim Œ±) :=
by
  suffices ‚àÄ F‚ÇÅ F‚ÇÇ : RubinFilter G, F‚ÇÅ ‚âà F‚ÇÇ ‚Üí ‚àÄ S : Set Œ±, S ‚àà nhds (F‚ÇÅ.lim Œ±) ‚Üí S ‚àà nhds (F‚ÇÇ.lim Œ±) by
    constructor
    apply this _ _ F_equiv
    apply this _ _ (Setoid.symm F_equiv)

  have basis := RegularSupportBasis.isBasis G Œ±

  intro F‚ÇÅ F‚ÇÇ F_equiv S S_in_nhds_F‚ÇÅ
  rw [basis.mem_nhds_iff] at S_in_nhds_F‚ÇÅ
  let ‚ü®T, T_in_basis, lim‚ÇÅ_in_T, T_ss_S‚ü© := S_in_nhds_F‚ÇÅ

  rw [basis.mem_nhds_iff]
  use T
  refine ‚ü®T_in_basis, ?lim‚ÇÇ_in_T, T_ss_S‚ü©

  rw [lim_mem_iff_of_eqv F_equiv T_in_basis] at lim‚ÇÅ_in_T
  exact lim‚ÇÅ_in_T

theorem RubinFilter.lim_eq_of_eqv {F‚ÇÅ F‚ÇÇ : RubinFilter G} (F_equiv : F‚ÇÅ ‚âà F‚ÇÇ) :
  F‚ÇÅ.lim Œ± = F‚ÇÇ.lim Œ± :=
by
  apply RubinFilter.le_nhds_eq_lim
  have nhds_lim_in_basis := nhds_in_basis (lim Œ± F‚ÇÅ) (RegularSupportBasis.isBasis G Œ±)

  apply UltrafilterInBasis.le_of_inf_neBot _ (RegularSupportBasis.closed_inter G Œ±) nhds_lim_in_basis

  constructor
  intro eq_bot

  rw [Filter.inf_eq_bot_iff] at eq_bot
  let ‚ü®U, U_in_F‚ÇÇ, V, V_in_nhds, UV_empty‚ü© := eq_bot

  rw [mem_nhds_lim_iff_of_eqv F_equiv] at V_in_nhds
  apply (F‚ÇÇ.map Œ±).ne_bot.ne
  rw [<-inf_eq_left.mpr (F‚ÇÇ.le_nhds_lim Œ±)]
  rw [Filter.inf_eq_bot_iff]
  exact ‚ü®U, U_in_F‚ÇÇ, V, V_in_nhds, UV_empty‚ü©

theorem RubinFilter.eqv_of_map_converges (F‚ÇÅ F‚ÇÇ : RubinFilter G) (p : Œ±):
  F‚ÇÅ.map Œ± ‚â§ nhds p ‚Üí F‚ÇÇ.map Œ± ‚â§ nhds p ‚Üí F‚ÇÅ ‚âà F‚ÇÇ :=
by
  intro F‚ÇÅ_le_nhds F‚ÇÇ_le_nhds
  intro S S_in_basis

  have F‚ÇÅ_rubinFilterOf := (RubinFilter.map_isRubinFilterOf F‚ÇÅ (Œ± := Œ±))
  have F‚ÇÇ_rubinFilterOf := (RubinFilter.map_isRubinFilterOf F‚ÇÇ (Œ± := Œ±))

  rw [<-AlgebraicCentralizerBasis.eq_rist_image (Œ± := Œ±)] at S_in_basis
  let ‚ü®S', S'_in_basis, S'_eq‚ü© := S_in_basis
  simp only at S'_eq
  rw [<-S'_eq]

  rw [<-F‚ÇÅ_rubinFilterOf.converges_iff S'_in_basis]
  rw [<-F‚ÇÇ_rubinFilterOf.converges_iff S'_in_basis]

  rw [F‚ÇÅ.exists_nhds_iff_lim_in_set Œ± S']
  rw [F‚ÇÇ.exists_nhds_iff_lim_in_set Œ± S']
  rw [<-F‚ÇÅ.le_nhds_eq_lim _ _ F‚ÇÅ_le_nhds]
  rw [<-F‚ÇÇ.le_nhds_eq_lim _ _ F‚ÇÇ_le_nhds]


lemma RubinFilter.fromPoint_converges' (p : Œ±) :
  ‚àÉ q : Œ±, (
  UltrafilterInBasis.of
    (RegularSupportBasis.closed_inter G Œ±)
    (nhds_in_basis p (RegularSupportBasis.isBasis G Œ±))
  ).filter ‚â§ nhds q :=
by
  use p
  apply UltrafilterInBasis.of_le

def RubinFilter.fromPoint (p : Œ±) : RubinFilter G :=
  RubinFilter.from (
    UltrafilterInBasis.of
      (RegularSupportBasis.closed_inter G Œ±)
      (nhds_in_basis p (RegularSupportBasis.isBasis G Œ±))
    )
    (RubinFilter.fromPoint_converges' p)

@[simp]
theorem RubinFilter.fromPoint_lim (p : Œ±) :
  (RubinFilter.fromPoint (G := G) p).lim Œ± = p :=
by
  symm
  apply RubinFilter.le_nhds_eq_lim
  unfold fromPoint
  rw [RubinFilter.map_from_eq]
  apply UltrafilterInBasis.of_le

theorem RubinFilter.lim_fromPoint_eqv (F : RubinFilter G) :
  RubinFilter.fromPoint (F.lim Œ±) ‚âà F :=
by
  apply eqv_of_map_converges _ _ (F.lim Œ±)
  ¬∑ unfold fromPoint
    rw [RubinFilter.map_from_eq]
    apply UltrafilterInBasis.of_le
  ¬∑ exact le_nhds_lim Œ± F

def RubinFilterBasis (G : Type _) [Group G] : Set (Set (RubinFilter G)) :=
  (fun S : Set G => { F : RubinFilter G | AlgebraicConvergent F.filter S }) '' AlgebraicCentralizerBasis G

instance : TopologicalSpace (RubinFilter G) := TopologicalSpace.generateFrom (RubinFilterBasis G)

theorem RubinFilterBasis.mem_iff (S : Set (RubinFilter G)) :
  S ‚àà RubinFilterBasis G ‚Üî ‚àÉ B ‚àà AlgebraicCentralizerBasis G, ‚àÄ F : RubinFilter G, F ‚àà S ‚Üî AlgebraicConvergent F.filter B :=
by
  unfold RubinFilterBasis
  simp
  conv => {
    lhs; congr; intro B; rhs
    rw [eq_comm, Set.ext_iff]
  }

def RubinSpace (G : Type _) [Group G] := Quotient (RubinFilterSetoid G)

def RubinSpace.fromPoint (p : Œ±) : RubinSpace G :=
  ‚ü¶RubinFilter.fromPoint p‚üß

instance : Membership (RubinFilter G) (RubinSpace G) where
  mem := fun F Q => ‚ü¶F‚üß = Q

theorem RubinSpace.mem_iff (F : RubinFilter G) (Q : RubinSpace G) :
  F ‚àà Q ‚Üî ‚ü¶F‚üß = Q := by rfl

theorem RubinSpace.fromPoint_converges (p : Œ±) :
  ‚àÄ F : RubinFilter G, F ‚àà RubinSpace.fromPoint (G := G) p ‚Üí F.lim Œ± = p :=
by
  intro F
  unfold fromPoint
  rw [mem_iff, Quotient.eq]
  intro F_eqv_from
  convert RubinFilter.lim_eq_of_eqv F_eqv_from
  clear F_eqv_from
  simp

noncomputable def RubinSpace.lim (Q : RubinSpace G) : Œ± :=
  Q.liftOn (RubinFilter.lim Œ±) (fun _a _b eqv => RubinFilter.lim_eq_of_eqv eqv)

theorem RubinSpace.lim_fromPoint (p : Œ±) :
  RubinSpace.lim (RubinSpace.fromPoint (G := G) p) = p :=
by
  unfold lim
  let ‚ü®Q, Q_eq‚ü© := (RubinSpace.fromPoint (G := G) p).exists_rep
  rw [<-Q_eq]
  simp
  apply RubinSpace.fromPoint_converges p Q
  rwa [mem_iff]

theorem RubinSpace.fromPoint_lim (Q : RubinSpace G) :
  RubinSpace.fromPoint (Q.lim (Œ± := Œ±)) = Q :=
by
  let ‚ü®Q', Q'_eq‚ü© := Q.exists_rep
  rw [<-Q'_eq, lim, fromPoint]
  simp
  rw [Quotient.eq]
  apply RubinFilter.lim_fromPoint_eqv

instance : TopologicalSpace (RubinSpace G) := by
  unfold RubinSpace
  infer_instance

theorem RubinSpace.lim_continuous : Continuous (RubinSpace.lim (G := G) (Œ± := Œ±)) := by
  sorry

theorem RubinSpace.fromPoint_continuous : Continuous (RubinSpace.fromPoint (G := G) (Œ± := Œ±)) := by
  sorry

/--
The canonical homeomorphism from a topological space that a rubin action acts on to
the rubin space.
--/
noncomputable def RubinSpace.homeomorph : Homeomorph (RubinSpace G) Œ± where
  toFun := RubinSpace.lim
  invFun := RubinSpace.fromPoint

  left_inv := RubinSpace.fromPoint_lim
  right_inv := RubinSpace.lim_fromPoint

  continuous_toFun := RubinSpace.lim_continuous
  continuous_invFun := RubinSpace.fromPoint_continuous

instance : MulAction G (RubinSpace G) := sorry

end Setoid

-- theorem rubin' (hŒ± : RubinAction G Œ±) : EquivariantHomeomorph G Œ± (RubinSpace G) where
--   toFun := fun x => ‚ü¶‚üß
--   invFun := fun S => sorry



end RubinFilter

/-
variable {Œ≤ : Type _}
variable [TopologicalSpace Œ≤] [MulAction G Œ≤] [ContinuousConstSMul G Œ≤]

theorem rubin (hŒ± : RubinAction G Œ±) (hŒ≤ : RubinAction G Œ≤) : EquivariantHomeomorph G Œ± Œ≤ := by
  -- by composing rubin' hŒ±
  sorry
-/

end Rubin

end Rubin
